# Compute first twelve Fibonacci numbers and put in array, then print
#
# Description:
#   This MIPS assembly program calculates the first 12 Fibonacci numbers,
#   stores them in an array, and then prints them to the console.
#
#   It uses a loop to compute the sequence iteratively:
#   F[n+2] = F[n] + F[n+1]
#
#   Registers used:
#   $t0 - Array address / Current element address
#   $t5 - Array size
#   $t2 - Temporary value for Fibonacci calculation
#   $t1 - Loop counter
#   $t3 - F[n]
#   $t4 - F[n+1]
#   $a0, $a1 - Arguments for system calls and function calls
#   $v0 - System call code

      .data
fibs: .word   0 : 13        # "array" of 12 words to contain fib values
size: .word  13             # size of "array" 
      .text
      la   $t0, fibs        # load address of array
      la   $t5, size        # load address of size variable
      lw   $t5, 0($t5)      # load array size
      li   $t2, 1           # 1 is first and second Fib. number
      add.d $f0, $f2, $f4
      sw   $t2, 0($t0)      # F[0] = 1
      sw   $t2, 4($t0)      # F[1] = F[0] = 1
      addi $t1, $t5, -2     # Counter for loop, will execute (size-2) times
loop: lw   $t3, 0($t0)      # Get value from array F[n] 
      lw   $t4, 4($t0)      # Get value from array F[n+1]
      add  $t2, $t3, $t4    # $t2 = F[n] + F[n+1]
      sw   $t2, 8($t0)      # Store F[n+2] = F[n] + F[n+1] in array
      addi $t0, $t0, 4      # increment address of Fib. number source
      addi $t1, $t1, -1     # decrement loop counter
      bgtz $t1, loop        # repeat if not finished yet.
      la   $a0, fibs        # first argument for print (array)
      add  $a1, $zero, $t5  # second argument for print (size)
      jal  print            # call print routine. 
      li   $v0, 10          # system call for exit
      syscall               # we are out of here.
		
#########  routine to print the numbers on one line. 
#
# print:
#   Iterates through an array of integers and prints them separated by spaces.
#
# Arguments:
#   $a0 - Address of the array
#   $a1 - Size of the array

      .data
space:.asciiz  " "          # space to insert between numbers
head: .asciiz  "The Fibonacci numbers are:\n"
      .text
print:add  $t0, $zero, $a0  # starting address of array
      add  $t1, $zero, $a1  # initialize loop counter to array size
      la   $a0, head        # load address of print heading
      li   $v0, 4           # specify Print String service
      syscall               # print heading
out:  lw   $a0, 0($t0)      # load fibonacci number for syscall
      li   $v0, 1           # specify Print Integer service
      syscall               # print fibonacci number
      la   $a0, space       # load address of spacer for syscall
      li   $v0, 4           # specify Print String service
      syscall               # output string
      addi $t0, $t0, 4      # increment address
      addi $t1, $t1, -1     # decrement loop counter
      bgtz $t1, out         # repeat if not finished
      jr   $ra              # return
